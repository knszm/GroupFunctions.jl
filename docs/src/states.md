# Basis states and GT patterns

This library computes matrix elements of SU(N) irreducible representations -- objects that appear, for instance, in quantum optics when describing how multi-photon states transform under passive linear networks. But matrix elements between which states? The answer involves Gelfand-Tsetlin (GT) patterns, an abstract scheme for labeling quantum states that in some cases allows for a physical interpretation.

Note that SU(N)  irreps are commonly identified by Young diagrams -- GT patterns hold this information ("which irrep", corresponding to the Young diagram) too, in addition to "which state within that irrep".

## SU(2): simple example

To understand GT patterns, we first need some familiarity with the underlying algebraic structure. SU(2) is the simplest case, so let us start there. The algebra su(2) is generated by 

```math
[J_z, J_\pm] = \pm J_\pm, \qquad [J_+, J_-] = 2 J_z.
```


States $|j, m\rangle$ with $m = -j, -j+1, \ldots, j$  form a basis for the spin-$j$
j irrep: $J_z |j,m\rangle = m |j,m\rangle$  and $J_\pm$ act as ladder operators. These states can be identified as spin (and angular momentum, for $j\in\mathbb{N}$) states.

But the same algebraic structure arises in a different physical setting. Consider two bosonic modes with creation operators   $a^\dagger_1, a^\dagger_2$. Define

```math
J_z = \tfrac{1}{2}(a^\dagger_1 a_1 - a^\dagger_2 a_2), \qquad J_+ = a^\dagger_1 a_2, \qquad J_- = a^\dagger_2 a_1.
```

One can verify these satisfy the commutation relations. Now $J_z$ measures the occupation difference between modes, and $J_\pm$ hop particles between them. A state with $n_1$ particles in mode 1 and $n_2$ in mode 2 has

```math
j = \tfrac{1}{2}(n_1 + n_2), \qquad m = \tfrac{1}{2}(n_1 - n_2).
```

The Fock state $|n_1, n_2\rangle$ is mathematically equivalent to the spin state $|j, m\rangle$ with $j$ and $m$ as above.


For SU(2), GT pattern is an inverse triangle of the form:
```math
\left|\begin{array}{ccc}
2j&&0\\
&m+j&
\end{array}\right\rangle.
```

 The top row specifies the total spin $j$, and the bottom row contains a single entry that determines $m$. We will see the general structure in the next section; for now, here is a quick look:

```julia
using GroupFunctions

# Irrep [2,0] of SU(2): spin-1, dimension 3
λ = [2, 0]
basis = basis_states(λ)

for b in basis
    display(b)
end
```


## SU(N): Multiple $J^{(l)}_z$ operators


For SU(N), we have $N-1$ commuting operators $J^{(l)}_z$ and corresponding ladder operators $J^{(l)}_\pm$. Each $J^{(l)}_z, J^{(l)}_\pm$ satisfies su(2) commutation relations among themselves. (See [Alex et al.](https://arxiv.org/abs/1009.0437) for explicit definitions.) For now, we will stick to the abstract view; as mentioned above, the operators and states can correspond to many different physical scenarios.

A GT pattern labels a simultaneous eigenstate of all $J^{(l)}_z$. The pattern is a triangular array:

```math
\left|\begin{array}{ccccccc}
m_{1,N} & & m_{2,N} & & \cdots & & m_{N,N} \\
& m_{1,N-1} & & \cdots & & m_{N-1,N-1} & \\
& & \ddots & & ~ & & \\
& & m_{1,2} & & m_{2,2} & & \\
& & & m_{1,1} & & &
\end{array}\right\rangle
```


The **top row** $(m_{1,N}, m_{2,N}, \ldots, m_{N,N})$ specifies the irrep: it corresponds to a Young diagram (integer partition). The remaining rows specify which state within that irrep it is, and are subject to the **betweenness condition**:

```math
m_{k, l+1} \geq m_{k, l} \geq m_{k+1, l+1},
```


```julia
using GroupFunctions

# Irrep [2,1,0] of SU(3): 8-dimensional
λ = [2, 1, 0]
basis = basis_states(λ)
println("Dimension: ", length(basis))

# Show first few patterns
for b in basis[1:3]
    display(b)
end
```

The **p-weight** of a pattern is the sequence of row-sum differences: if $\sigma_l = \sum_k m_{k,l}$, then $w_l = \sigma_l - \sigma_{l-1}$. For certain irreps, this has a direct physical interpretation.

The *symmetric irrep* $\lambda = [N, 0, \ldots, 0]$ (single-row Young diagram) describes $N$ indistinguishable bosons in $d$ modes. For this irrep, the p-weight directly gives the occupation numbers $(n_1, n_2, \ldots, n_d)$.

```julia
# 2 photons in 3 modes
λ = [2, 0, 0]
basis = basis_states(λ)

for b in basis
    println(b, " → occupation ", pweight(b)) # will output all Fock states of 3 modes with total number of particles = 2
end
```

Each familiar Fock state corresponds to exactly one GT pattern.


The antisymmetric irrep $\lambda = [1, 1, \ldots, 1,0,0,\ldots,0]$ (single-column Young diagram) describes fermions. Pauli exclusion restricts occupations to 0 or 1.

```julia
# 2 fermions in 3 modes
λ = [1, 1, 0]
basis = basis_states(λ)
println("Dimension: ", length(basis))  # Should be 3

for b in basis
    println(b, " → occupation ", pweight(b)) # will output all fermionic states -- in Fock space, permutations of [1,1,0] 
end
```

What about irreps like $[2, 1]$? These are neither fully symmetric nor fully antisymmetric. They arise physically when particles are *partially distinguishable*.

Consider three photons entering an interferometer.  Two arrive at the same time, with identical spectral properties: they can fully interfere within the interferometer. The third  photon arrives later and can not interfere with the earlier photon pair. The resulting quantum state is not confined to a single irrep; it has components in both the symmetric sector $[3,0,0]$ and the mixed-symmetry sector $[2,1,0]$. 
This decomposition manifests in observable coincidence rates; see Section 3.2 of [D. Amaro Alcalá et al.](https://arxiv.org/abs/2004.11504) for the explicit calculation.

The simpler case of two photons with partial overlap similarly involves both $[2]$ (symmetric, contributing via the permanent) and $[1,1]$ (antisymmetric, contributing via the determinant); see Section 2.1 of the above paper.

```julia
# Mixed irrep [2,1,0]: dimension 8
λ = [2, 1, 0]
basis = basis_states(λ)

for b in basis
    println(pweight(b))
end
# Notice: p-weight (1,1,1) appears twice — inner multiplicity!
```

For mixed irreps, multiple GT patterns can share the same p-weight. This inner multiplicity reflects that occupation numbers alone do not uniquely specify a state when particles have mixed exchange symmetry.


Now that we have a labeling scheme for basis states, the natural question is: how do transition amplitudes between these states depend on the unitary transformation $U$? For bosons, this involves permanents; for fermions, determinants; for mixed symmetry, a generalization called the Grabmeier-Kerber formula. This is the topic of [the next section](group_functions.md).

